buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
    }
}

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'java'
apply plugin: 'ivy-publish'

version = '1.0'
group = 'com.crankycoder'
description = "A KML serializer for Android"

dependencies {
    compile files('libs/simple-xml-2.6.3.jar')
}

sourceSets {
    main {
        java.srcDirs = ['src']
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.java
    classifier "source"
}

publishing {
    repositories {
        jcenter()
        ivy {
            url "${rootProject.buildDir}/repo"
        }
    }
    publications {
        ivy(IvyPublication) {
            from components.java
            artifact(sourceJar) {
                type "source"
                conf "runtime"
            }
            descriptor.withXml {
                asNode().info[0].appendNode('description', description)
            }
        }

        mavenJava(MavenPublication) {
            from components.java
            artifact(sourceJar) {
                classifier "sources"
            }
            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'SimpleKML')
                root.appendNode('url', 'http://github.com/crankycoder/SimpleKML')
            }
        }

    }
}

compileJava {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}


bintray {
    user = ''
    key = ''
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'SimpleKML'
        name = 'SimpleKML'
        vcsUrl = 'https://github.com/crankycoder/SimpleKML.git'
        licenses = ['Apache-2.0']
        publicDownloadNumbers = true
    }
}
